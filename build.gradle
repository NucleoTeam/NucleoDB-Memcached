plugins {
    id 'java'
    id 'maven-publish'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'jacoco'
}

group = "com.nucleodb.memcache"

repositories {
    mavenCentral()
    maven {
        name = 'GitHubPackages'
        url = uri("https://nexus.synload.com/repository/maven-repo-releases/")
    }
}

dependencies {
    implementation 'com.nucleodb:library:1.13.21'
    implementation 'net.spy:spymemcached:2.12.3'
    implementation 'io.netty:netty-all:4.1.101.Final'
    implementation 'org.apache.logging.log4j:log4j-api'
    implementation 'org.apache.logging.log4j:log4j-core'
    testImplementation 'junit:junit:4.13.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'

}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
    environment "TEST2", "CHECK"
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

application {
    mainClass = 'svs.memcached.server.MemcachedServer'
}

shadowJar {
    archiveBaseName.set(group)
    archiveVersion.set(version)
    archiveClassifier.set('')

    manifest {
        attributes 'Main-Class': 'svs.memcached.server.MemcachedServer'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar

            // Additional configuration, e.g., add artifacts, set up pom details, etc.
            pom {
                name = 'NucleoDB Memcache Server'
                description = 'Memcache compatible nucleodb server'
                url = 'https://github.com/NucleoTeam/NucleoDB'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'firestar'
                        name = 'Nathaniel Davidson'
                        email = 'nathaniel.davidson@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/NucleoTeam/NucleoDB.git'
                    developerConnection = 'scm:git:ssh://github.com/NucleoTeam/NucleoDB.git'
                    url = 'https://github.com/NucleoTeam/NucleoDB/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'SynloadRepo'
            url = uri("https://nexus.synload.com/repository/maven-repo-releases/")
            credentials {
                username = System.getenv("SYNLOAD_REPO_USER")
                password = System.getenv("SYNLOAD_REPO_PASS")
            }
        }
    }
}